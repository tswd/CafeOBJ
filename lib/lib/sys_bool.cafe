**
** MODULE BOOL
** 
** we don't want to include BOOL of course

set include BOOL off

sys:mod! BOOL
      principal-sort Bool
{
  imports {
    protecting (BASE-BOOL)
  }
  axioms {
    var A : Bool
    var B : Bool
    var C : Bool
    eq false and A = false .
    eq true and A = A .
    eq A and A = A .
    eq A xor A = false .
    eq false xor A = A .
    eq A and (B xor C) = A and B xor A and C .
    eq A or A = A .
    eq false or A = A .
    eq true or A = true .
    eq A or B = A and B xor A xor B .
    eq not A = A xor true .
    eq A implies B = A and B xor A xor true .
    eq A iff B = A xor B xor true .
    eq A and-also false = false .
    eq false and-also A = false .
    eq A and-also true = A .
    eq true and-also A = A .
    eq A and-also A = A .
    eq false or-else A = A .
    eq A or-else false = A .
    eq true or-else A = true .
    eq A or-else true = true .
  }
}
